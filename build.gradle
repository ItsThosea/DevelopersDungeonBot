plugins {
    id "java-library"
    id "io.github.goooler.shadow" version "8.1.7"
}

group = "me.thosea"
description = "DevelopersDungeon"

// A.B.C-D
// A - major new feature
// B - minor new feature
// C - small changes
// D - bug fixes
version = "1.4.10-0"

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    api("net.dv8tion:JDA:5.2.1") {
        // exclude audio library
        exclude group: "club.minnced", module: "opus-java"
    }
    api "it.unimi.dsi:fastutil-core:8.5.13"
    api "org.slf4j:slf4j-simple:2.0.13"
    api "com.google.code.gson:gson:2.11.0"
}

processResources {
    filesMatching("devdungeon.properties") {
        expand(version: version, token: System.getenv("DEVDUNGEON_TOKEN"))
    }
}

java.toolchain {
    languageVersion.set(JavaLanguageVersion.of(22))
}

shadowJar {
    minimize()
    include "**"

    exclude "META-INF/**"
    include "META-INF/MANIFEST.MF"
    include "META-INF/devdungeon.properties"

    // library only used for audio
    exclude "com/iwebpp/**"

    archiveFileName = "DevelopersDungeon-${version}.jar"
    tasks.build.finalizedBy tasks.shadowJar
    doLast { tasks.jar.archiveFile.get().asFile.delete() }
}

jar {
    archiveFileName = "DevelopersDungeon-${version}-no_deps.jar"
    manifest {
        attributes "Main-Class": "me.thosea.developersdungeon.Main"
    }
}