plugins {
    id "java-library"
    id "io.github.goooler.shadow" version "8.1.7"
}

group = "me.thosea"
version = "1.4.9"
description = "DevelopersDungeon"

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    api("net.dv8tion:JDA:5.0.0-beta.24") {
        // exclude audio library
        exclude group: "club.minnced", module: "opus-java"
    }
    api "it.unimi.dsi:fastutil-core:8.5.13"
    api "org.slf4j:slf4j-simple:2.0.13"
    api "com.google.code.gson:gson:2.11.0"
}

processResources {
    filesMatching("devdungeon.properties") {
        expand(version: version, token: System.getenv("DEVDUNGEON_TEST_TOKEN"))
    }
}

java.toolchain {
    languageVersion.set(JavaLanguageVersion.of(22))
}

shadowJar {
    minimize()
    include "**"

    exclude "META-INF/**"
    include "META-INF/MANIFEST.MF"
    include "META-INF/devdungeon.properties"

    // library only used for audio
    exclude "com/iwebpp/**"

    archiveFileName = "DevelopersDungeon-${version}-dev.jar"
    tasks.build.finalizedBy tasks.shadowJar
    doLast { tasks.jar.archiveFile.get().asFile.delete() }
}

jar {
    archiveFileName = "DevelopersDungeon-${version}-dev-no_deps.jar"
    manifest {
        attributes "Main-Class": "me.thosea.developersdungeon.Main"
    }
}